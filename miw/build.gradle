/********************************************************************************
 * Copyright (c) 2024 Contributors to the Eclipse Foundation
 *
 * See the NOTICE file(s) distributed with this work for additional
 * information regarding copyright ownership.
 *
 * This program and the accompanying materials are made available under the
 * terms of the Apache License, Version 2.0 which is available at
 * https://www.apache.org/licenses/LICENSE-2.0.
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations
 * under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 ********************************************************************************/

plugins {
    id 'java'
    id "jacoco"
    id 'project-report'
    id "de.undercouch.download" version "5.5.0"
}

group = "${groupName}"
version = "${applicationVersion}"
sourceCompatibility = JavaVersion.VERSION_17

// alias for Project.getConfigurations()
configurations {
    // add a custom config to avoid applying the dev-tools to a production app
    developmentOnly
    runtimeClasspath.extendsFrom(developmentOnly)

    // adding a configuration 'compileOnly' which extends from annotationProcessor
    compileOnly.extendsFrom(annotaionProcessor)
}


dependencies {

    //project deps
    implementation project(":wallet-commons")

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security:spring-security-oauth2-resource-server'
    implementation 'org.springframework.security:spring-security-oauth2-jose'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation "org.springdoc:springdoc-openapi-starter-common:${openApiVersion}"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${openApiVersion}"
    implementation "com.smartsensesolutions:commons-dao:${commonsDaoVersion}"
    implementation 'org.liquibase:liquibase-core'
    implementation "org.eclipse.tractusx.ssi:cx-ssi-lib:${ssiLibVersion}"

    runtimeOnly 'org.postgresql:postgresql'

    testImplementation 'org.wiremock:wiremock-standalone:3.4.2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.testcontainers:testcontainers"
    testImplementation 'com.h2database:h2:2.2.220'
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation group: 'com.github.dasniko', name: 'testcontainers-keycloak', version: '2.5.0'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '5.2.0'
    testImplementation group: 'org.json', name: 'json', version: '20230227'
    testImplementation group: 'com.github.curious-odd-man', name: 'rgxgen', version: '1.4'
    testImplementation "com.teketik:mock-in-bean:${mockInBeanVersion}"
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom "org.testcontainers:testcontainers-bom:${testContainerVersion}"
    }
}

build {
    archivesBaseName = "miw"
    version = "latest"
}

bootJar {
    enabled = true
    metaInf {
        from 'DEPENDENCIES'
        from '../SECURITY.md'
        from '../NOTICE.md'
        from '../LICENSE'
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

htmlDependencyReport {
    projects = project.allprojects
}

jacocoTestReport {

    reports {
        xml.enabled true
        xml.outputLocation = file("./build/reports/xml/jacoco.xml")

        csv.enabled false

        html.enabled true
        html.outputLocation = file("./build/reports/html/jacoco")
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "org/eclipse/tractusx/managedidentitywallets/dto/*",
                    "org/eclipse/tractusx/managedidentitywallets/dao/entity/*",
                    "org/eclipse/tractusx/managedidentitywallets/constant/*",
                    "org/eclipse/tractusx/managedidentitywallets/exception/*"
            ])
        }))
    }
}

jacoco {
    toolVersion = "${jacocoVersion}"
}


jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "org/eclipse/tractusx/managedidentitywallets/dto/*",
                    "org/eclipse/tractusx/managedidentitywallets/dao/entity/*",
                    "org/eclipse/tractusx/managedidentitywallets/constant/*",
                    "org/eclipse/tractusx/managedidentitywallets/exception/*"
            ])
        }))
    }
    violationRules {
        rule {
            limit {
                // disabled for now
                minimum = 0.00
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification
